---
title: 操作系统的启动
date: 2017-02-02 22:39:59
tags: 操作系统
---
x86 PC刚开机时CPU处于实模式，CS=0xFFFF，PC=0x0000，实模式寻址方式为CS左移4位+PC。因此寻址0xFFFF0(ROM BIOS映射区)，
BIOS检查RAM，键盘，显示器，磁盘，并将磁盘0磁道0扇区（操作系统的引导扇区）读入512字节至0x7c00处，设置CS=0x07c0，IP=0x0000
<!--more-->
### 引导扇区代码：bootsect.s
bootsect.s将setup和system分别装入内存，并在屏幕上打印出欢迎信息，以下为linux0.11的部分源码
```
SETUPLEN = 4				! nr of setup-sectors
BOOTSEG  = 0x07c0			! original address of boot-sector
INITSEG  = 0x9000			! we move boot here - out of the way
SETUPSEG = 0x9020			! setup starts here
SYSSEG   = 0x1000			! system loaded at 0x10000 (65536).
ENDSEG   = SYSSEG + SYSSIZE		! where to stop loading

! ROOT_DEV:	0x000 - same type of floppy as boot.
!		0x301 - first partition on first drive etc
ROOT_DEV = 0x306

entry _start    //告诉链接器程序入口
_start:
	mov	ax,#BOOTSEG    //0x7c00处存放的语句
	mov	ds,ax          //ds=0x07c0
	mov	ax,#INITSEG
	mov	es,ax          //es=0x9000
	mov	cx,#256
	sub	si,si          //si=0
	sub	di,di          //di=0
    rep    //重复移动cx(256)个字
	movw   //将ds:si(0x07c0:0x0000)处的256个字移动到es:di(0x9000:0x0000)
	jmpi	go,INITSEG    //jmpi间接跳转，将go赋值给PC，INITSETG赋值给CS，跳转到下面一条指令
go:	mov	ax,cs
	mov	ds,ax
	mov	es,ax    //es=0x9000
! put stack at 0x9ff00.
	mov	ss,ax
	mov	sp,#0xFF00		! arbitrary value >>512

! load the setup-sectors directly after the bootblock.
! Note that 'es' is already set up.

load_setup:    //载入setup模块
	mov	dx,#0x0000		! drive 0, head 0
	mov	cx,#0x0002		! sector 2, track 0
	mov	bx,#0x0200		! address = 512, in INITSEG
    mov	ax,#0x0200+SETUPLEN	! service 2, nr of sectors    SETUPLEN=4，读al(4)个扇区，读到es:bs(0x90200,bootset的后面）
	int	0x13			! read it   //读磁盘中断，ah=0x02读磁盘，al=扇区数量，ch=柱面号，cl=开始扇区，dh=磁头号，dl=驱动器号，ex:bx=内存地址
	jnc	ok_load_setup		! ok - continue
	mov	dx,#0x0000
	mov	ax,#0x0000		! reset the diskette
	int	0x13
	j	load_setup

ok_load_setup:

! Get disk drive parameters, specifically nr of sectors/track

	mov	dl,#0x00
	mov	ax,#0x0800		! AH=8 is get drive parameters
	int	0x13
	mov	ch,#0x00
	seg cs
	mov	sectors,cx
	mov	ax,#INITSEG
	mov	es,ax

! Print some inane message

	mov	ah,#0x03		! read cursor pos
	xor	bh,bh
	int	0x10
	
	mov	cx,#24    //输出24个字符
	mov	bx,#0x0007		! page 0, attribute 7 (normal)
	mov	bp,#msg1
	mov	ax,#0x1301		! write string, move cursor
	int	0x10     //显示字符，把msg1打印到光标的位置

! ok, we've written the message, now
! we want to load the system (at 0x10000)

	mov	ax,#SYSSEG
	mov	es,ax		! segment of 0x010000
	call	read_it    //读入system模块
	call	kill_motor

! After that we check which root-device to use. If the device is
! defined (!= 0), nothing is done and the given device is used.
! Otherwise, either /dev/PS0 (2,28) or /dev/at0 (2,8), depending
! on the number of sectors that the BIOS reports currently.

	seg cs
	mov	ax,root_dev
	cmp	ax,#0
	jne	root_defined
	seg cs
	mov	bx,sectors
	mov	ax,#0x0208		! /dev/ps0 - 1.2Mb
	cmp	bx,#15
	je	root_defined
	mov	ax,#0x021c		! /dev/PS0 - 1.44Mb
	cmp	bx,#18
	je	root_defined
undef_root:
	jmp undef_root
root_defined:
	seg cs
	mov	root_dev,ax

! after that (everyting loaded), we jump to
! the setup-routine loaded directly after
! the bootblock:

	jmpi	0,SETUPSEG    //跳转至0x90200执行setup.s


.
.
.



msg1:
	.byte 13,10
	.ascii "Loading system ..."
	.byte 13,10,13,10


```

